AC_PREREQ(2.53)
AC_INIT(src)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(zenity, 2.11.0)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL([0.29])

AC_PROG_CC

GTK_REQUIRED=2.3.1

PKG_CHECK_MODULES(ZENITY, gtk+-2.0 >= $GTK_REQUIRED libglade-2.0 libgnomecanvas-2.0 glib-2.0 >= 2.5.3)
AC_SUBST(ZENITY_CFLAGS)
AC_SUBST(ZENITY_LIBS)

dnl ***********************************************
dnl scrollkeeper checks
dnl ***********************************************

AC_PATH_PROG(SCROLLKEEPER_CONFIG, scrollkeeper-config,no)
if test x$SCROLLKEEPER_CONFIG = xno; then
	AC_MSG_ERROR(Couldn't find scrollkeeper-config. Please install the scrollkeeper package)
fi

dnl ***********************************************
dnl X development libraries check
dnl ***********************************************
                                                                                                                                                             
#
# If Pango included the shared library dependencies from X11 in
# the pkg-config output, then we use that (to avoid duplicates).
# but if they were omitted to avoid binary compatibility problems
# then we need to repeat the checks.
#
if $PKG_CONFIG --exists pangoxft ; then
  PANGO_PACKAGES="pangox pangoxft"
else
  PANGO_PACKAGES="pangox"
fi
                                                                                                                                                             
x_libs="`$PKG_CONFIG --libs $PANGO_PACKAGES`"
case x_libs in
  *-lX11*) pango_omitted_x_deps=no ;;
  *)       pango_omitted_x_deps=yes ;;
esac
                                                                                                                                                             
if test $pango_omitted_x_deps = yes ; then
  AC_PATH_XTRA
                                                                                                                                                             
  if test x$no_x = xyes ; then
    AC_MSG_ERROR([X development libraries not found])
  else
    X_LIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
  fi
fi
                                                                                                                                                             
AC_SUBST(X_LIBS)

dnl *******************************
dnl perl check
dnl *******************************

AC_PATH_PROG(PERL,perl,)

dnl *******************************
dnl Internationalization
dnl ******************************* 

ALL_LINGUAS="am ar az be bg bn bs ca cs cy da de el en_CA en_GB es et eu fa fi fr ga gl gu hi hr hu id is it ja kn ko lt mi mk ml mn ms nb ne nl nn no pa pl pt pt_BR ro ru sk sl sq sr sr@Latn sv ta th tr uk vi zh_CN zh_TW"
GETTEXT_PACKAGE=zenity-0.1
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT

dnl **************************************************************
dnl AM_GLIB_GNU_GETTEXT above substs $DATADIRNAME
dnl this is the directory where the *.{mo,gmo} files are installed
dnl **************************************************************

zenitylocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(zenitylocaledir)

AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Finding gettext package])

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${prefix}/share/pixmaps", [Determining icon location])

AC_OUTPUT([
Makefile 
zenity.spec
po/Makefile.in
src/Makefile
src/gdialog
data/Makefile
help/Makefile
help/C/Makefile
help/sr/Makefile
])
